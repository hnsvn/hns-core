# Copyright (c) 2022 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/build/config.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")

buildflag_header("sparkle_buildflags") {
  header = "sparkle_buildflags.h"
  flags = [ "ENABLE_SPARKLE=$enable_sparkle" ]
}

if (enable_sparkle && !build_sparkle) {
  bundle_data("prebuilt_sparkle_framework_bundle_data") {
    sources = [ "//build/mac_files/sparkle_binaries/Sparkle.framework" ]
    outputs = [ "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}" ]
  }
}

source_set("browser_process") {
  visibility = [
    "//hns/browser/*",
    "//chrome/browser/*",
  ]

  sources = [ "hns_browser_process.h" ]

  deps = [
    "//hns/browser/ethereum_remote_client/buildflags",
    "//hns/components/hns_vpn/common/buildflags",
    "//hns/components/greaselion/browser/buildflags",
    "//hns/components/ipfs/buildflags",
    "//hns/components/request_otr/common/buildflags",
    "//hns/components/speedreader/common/buildflags",
    "//hns/components/tor/buildflags",
    "//chrome/browser:browser_process",
    "//extensions/buildflags",
  ]
}

group("browser") {
  visibility = [
    "//hns:browser_dependencies",
    "//hns/browser/*",
    "//hns/components/hns_rewards/core:headers",
    "//hns/test:*",

    # these are layering violation and need to be fixed
    "//hns/components/hns_rewards/browser:testutil",
    "//hns/components/hns_rewards/test:hns_rewards_unit_tests",
    "//hns/components/ipfs/test:*",
  ]

  if (enable_sparkle) {
    if (build_sparkle) {
      deps = [ "//hns/vendor/sparkle:sparkle_framework_bundle_data" ]
    } else {
      deps = [ ":prebuilt_sparkle_framework_bundle_data" ]
    }
  }

  # Group depending on upstream's counterpart to make it easier to manage
  # dependencies in hns/ (//chrome/browser does not depend on this target
  # so //hns/browser becomes essentially an alias for //chrome/browser now).
  public_deps = [ "//chrome/browser" ]
}

source_set("unit_tests") {
  testonly = true

  sources = []
  deps = []

  if (is_win) {
    sources += [ "default_protocol_handler_utils_win_unittest.cc" ]

    deps += [
      "//base",
      "//hns/browser",
      "//testing/gtest",
    ]
  }
}
