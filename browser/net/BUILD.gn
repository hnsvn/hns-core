# Copyright (c) 2017 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/components/hns_webtorrent/browser/buildflags/buildflags.gni")
import("//hns/components/ipfs/buildflags/buildflags.gni")
import("//hns/components/tor/buildflags/buildflags.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")

declare_args() {
  hns_google_api_key = ""
  hns_google_api_endpoint = ""
}

buildflag_header("geolocation") {
  header = "hns_geolocation_buildflags.h"

  flags =
      [ "GOOGLEAPIS_URL=\"$hns_google_api_endpoint$hns_google_api_key\"" ]
}

source_set("net") {
  # Remove when https://github.com/hnsvn/hns-browser/issues/10659 is resolved
  check_includes = false

  sources = [
    "hns_ad_block_csp_network_delegate_helper.cc",
    "hns_ad_block_csp_network_delegate_helper.h",
    "hns_ad_block_tp_network_delegate_helper.cc",
    "hns_ad_block_tp_network_delegate_helper.h",
    "hns_ads_status_header_network_delegate_helper.cc",
    "hns_ads_status_header_network_delegate_helper.h",
    "hns_block_safebrowsing_urls.cc",
    "hns_block_safebrowsing_urls.h",
    "hns_common_static_redirect_network_delegate_helper.cc",
    "hns_common_static_redirect_network_delegate_helper.h",
    "hns_httpse_network_delegate_helper.cc",
    "hns_httpse_network_delegate_helper.h",
    "hns_localhost_permission_network_delegate_helper.cc",
    "hns_localhost_permission_network_delegate_helper.h",
    "hns_proxying_url_loader_factory.cc",
    "hns_proxying_url_loader_factory.h",
    "hns_proxying_web_socket.cc",
    "hns_proxying_web_socket.h",
    "hns_reduce_language_network_delegate_helper.cc",
    "hns_reduce_language_network_delegate_helper.h",
    "hns_referrals_network_delegate_helper.cc",
    "hns_referrals_network_delegate_helper.h",
    "hns_request_handler.cc",
    "hns_request_handler.h",
    "hns_service_key_network_delegate_helper.cc",
    "hns_service_key_network_delegate_helper.h",
    "hns_site_hacks_network_delegate_helper.cc",
    "hns_site_hacks_network_delegate_helper.h",
    "hns_static_redirect_network_delegate_helper.cc",
    "hns_static_redirect_network_delegate_helper.h",
    "hns_stp_util.cc",
    "hns_stp_util.h",
    "hns_system_request_handler.cc",
    "hns_system_request_handler.h",
    "decentralized_dns_network_delegate_helper.cc",
    "decentralized_dns_network_delegate_helper.h",
    "global_privacy_control_network_delegate_helper.cc",
    "global_privacy_control_network_delegate_helper.h",
    "resource_context_data.cc",
    "resource_context_data.h",
    "url_context.cc",
    "url_context.h",
  ]

  deps = [
    ":geolocation",
    "//base",
    "//hns/app:hns_generated_resources_grit",
    "//hns/browser/safebrowsing",
    "//hns/common",
    "//hns/components/hns_ads/browser",
    "//hns/components/hns_component_updater/browser",
    "//hns/components/hns_referrals/browser",
    "//hns/components/hns_shields/browser",
    "//hns/components/hns_shields/common",
    "//hns/components/hns_wallet/browser",
    "//hns/components/hns_wallet/common",
    "//hns/components/hns_webtorrent/browser/buildflags",
    "//hns/components/constants",
    "//hns/components/constants:hns_service_key_helper",
    "//hns/components/decentralized_dns/content",
    "//hns/components/ipfs/buildflags",
    "//hns/components/update_client:buildflags",
    "//hns/components/widevine:static_buildflags",
    "//hns/extensions:common",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/proxy_config",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network:network_service",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/re2",
    "//url",
  ]

  if (enable_extensions) {
    deps += [ "//extensions/common:common_constants" ]
  }

  if (enable_ipfs) {
    sources += [
      "ipfs_redirect_network_delegate_helper.cc",
      "ipfs_redirect_network_delegate_helper.h",
    ]
    deps += [
      "//hns/components/ipfs",
      "//chrome/common:channel_info",
    ]
  }

  if (enable_hns_webtorrent) {
    sources += [
      "hns_torrent_redirect_network_delegate_helper.cc",
      "hns_torrent_redirect_network_delegate_helper.h",
    ]

    deps += [
      "//hns/components/hns_webtorrent/browser/",
      "//extensions/browser",
    ]
  }
}

source_set("browser_tests") {
  testonly = true

  sources = [
    "hns_accept_header_browsertest.cc",
    "hns_network_delegate_browsertest.cc",
    "hns_network_delegate_hsts_fingerprinting_browsertest.cc",
    "hns_service_key_network_delegate_helper_browsertest.cc",
    "hns_site_hacks_network_delegate_helper_browsertest.cc",
    "hns_system_request_handler_browsertest.cc",
    "global_privacy_control_network_delegate_helper_browsertest.cc",
  ]
  deps = [
    "//hns/browser/net",
    "//hns/components/hns_shields/browser",
    "//hns/components/hns_shields/common",
    "//hns/components/query_filter",
    "//hns/components/tor/buildflags",
    "//chrome/browser",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//services/network/public/cpp",
  ]
  if (!is_android) {
    deps += [ "//chrome/test:test_support_ui" ]
  }
  if (is_android) {
    deps += [
      "//chrome/test:test_support_ui_android",
      "//net:test_support",
      "//net/traffic_annotation:test_support",
    ]
  }
  if (enable_tor) {
    deps += [
      "//hns/browser/tor",
      "//hns/components/tor",
    ]
  }
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "hns_ad_block_tp_network_delegate_helper_unittest.cc",
    "hns_ads_status_header_network_delegate_helper_unittest.cc",
    "hns_block_safebrowsing_urls_unittest.cc",
    "hns_common_static_redirect_network_delegate_helper_unittest.cc",
    "hns_httpse_network_delegate_helper_unittest.cc",
    "hns_network_delegate_base_unittest.cc",
    "hns_site_hacks_network_delegate_helper_unittest.cc",
    "hns_static_redirect_network_delegate_helper_unittest.cc",
    "hns_system_request_handler_unittest.cc",
  ]

  deps = [
    "//hns/browser/net",
    "//hns/browser/net:geolocation",
    "//hns/components/hns_ads/browser",
    "//hns/components/hns_ads/browser:test_support",
    "//hns/components/hns_component_updater/browser",
    "//hns/components/hns_rewards/browser",
    "//hns/components/hns_rewards/common",
    "//hns/components/hns_rewards/common:features",
    "//hns/components/hns_shields/browser",
    "//hns/components/l10n/common:test_support",
    "//chrome/test:test_support",
    "//content/test:test_support",
    "//net",
    "//net:test_support",
    "//services/network:network_service",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/abseil-cpp:absl",
  ]

  if (enable_hns_webtorrent) {
    sources += [
      "//hns/browser/extensions/hns_webtorrent_navigation_throttle_unittest.cc",
      "//hns/browser/net/hns_torrent_redirect_network_delegate_helper_unittest.cc",
    ]

    deps += [
      "//hns/browser/extensions",
      "//hns/components/hns_referrals/browser",
    ]
  }

  if (!is_android) {
    sources += [
      # TODO(samartnik): this should work on Android, we will review it once unit tests are set up on CI
      "hns_referrals_network_delegate_helper_unittest.cc",
    ]
    deps += [ "//hns/components/hns_referrals/browser" ]
  }
}
