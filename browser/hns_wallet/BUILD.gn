# Copyright (c) 2019 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//hns/components/ipfs/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")
import("//testing/test.gni")

source_set("hns_wallet") {
  sources = [
    "asset_ratio_service_factory.cc",
    "asset_ratio_service_factory.h",
    "bitcoin_wallet_service_factory.cc",
    "bitcoin_wallet_service_factory.h",
    "blockchain_images_source.cc",
    "blockchain_images_source.h",
    "hns_wallet_context_utils.cc",
    "hns_wallet_context_utils.h",
    "hns_wallet_ipfs_service_factory.cc",
    "hns_wallet_ipfs_service_factory.h",
    "hns_wallet_service_factory.cc",
    "hns_wallet_service_factory.h",
    "json_rpc_service_factory.cc",
    "json_rpc_service_factory.h",
    "keyring_service_factory.cc",
    "keyring_service_factory.h",
    "simulation_service_factory.cc",
    "simulation_service_factory.h",
    "swap_service_factory.cc",
    "swap_service_factory.h",
    "tx_service_factory.cc",
    "tx_service_factory.h",
  ]
  if (enable_ipfs_local_node) {
    sources += [
      "hns_wallet_auto_pin_service_factory.cc",
      "hns_wallet_auto_pin_service_factory.h",
      "hns_wallet_pin_service_factory.cc",
      "hns_wallet_pin_service_factory.h",
    ]
  }
  if (!is_android) {
    sources += [ "wallet_notification_helper.h" ]
  }
  deps = [
    "//hns/browser/profiles:util",
    "//hns/components/hns_wallet/browser",
    "//hns/components/hns_wallet/browser:constants",
    "//hns/components/hns_wallet/browser:utils",
    "//hns/components/hns_wallet/common",
    "//hns/components/hns_wallet/common:mojom",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profiles",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/user_prefs",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//services/network/public/cpp",
    "//third_party/abseil-cpp:absl",
  ]
  if (enable_extensions) {
    deps += [
      "//hns/browser/extensions",
      "//chrome/browser/extensions:extensions",
      "//extensions/browser",
    ]
  }
}

source_set("hns_wallet_delegate") {
  sources = [
    "hns_wallet_provider_delegate_impl.cc",
    "hns_wallet_provider_delegate_impl.h",
    "hns_wallet_service_delegate_helper.cc",
  ]
  deps = [
    "//hns/browser/hns_wallet",
    "//hns/browser/hns_wallet:tab_helper",
    "//hns/components/hns_wallet/browser",
    "//hns/components/hns_wallet/browser:constants",
    "//hns/components/hns_wallet/browser:permission_utils",
    "//hns/components/hns_wallet/browser:utils",
    "//hns/components/hns_wallet/common:mojom",
    "//hns/components/resources:strings_grit",
    "//chrome/browser/profiles:profile",
    "//components/content_settings/core/common",
    "//components/permissions",
    "//components/permissions:permissions",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
  ]
  if (is_android) {
    sources += [
      "hns_wallet_provider_delegate_impl_helper_android.cc",
      "hns_wallet_service_delegate_impl_android.cc",
      "hns_wallet_service_delegate_impl_android.h",
    ]
    deps += [
      "//hns/build/android:jni_headers",
      "//chrome/browser/ui:tab_model_enums_java",
      "//chrome/browser/ui:ui",
    ]
  } else {
    sources += [
      "hns_wallet_provider_delegate_impl_helper.cc",
      "hns_wallet_service_delegate_impl.cc",
      "hns_wallet_service_delegate_impl.h",
    ]
    deps += [ "//chrome/browser/ui" ]
    if (enable_extensions) {
      deps += [ ":external_wallets_importer" ]
    }
  }
}

source_set("external_wallets_importer") {
  sources = [
    "external_wallets_importer.cc",
    "external_wallets_importer.h",
  ]
  deps = [
    "//hns/browser/ethereum_remote_client/buildflags",
    "//hns/components/hns_wallet/browser",
    "//hns/components/hns_wallet/common",
    "//hns/components/hns_wallet/common:mojom",
    "//hns/third_party/argon2",
    "//components/value_store:value_store",
  ]
  if (enable_extensions) {
    deps += [
      "//extensions/browser",
      "//extensions/common",
    ]
  }
  if (ethereum_remote_client_enabled) {
    deps += [
      "//hns/browser/ethereum_remote_client",
      "//hns/browser/ethereum_remote_client:browser",
    ]
  }
}

source_set("tab_helper") {
  sources = [
    "hns_wallet_tab_helper.cc",
    "hns_wallet_tab_helper.h",
  ]

  deps = [
    "//hns/common",
    "//hns/components/hns_wallet/browser:permission_utils",
    "//components/permissions",
    "//components/sessions",
    "//content/public/browser",
  ]

  if (!is_android) {
    deps += [ "//hns/browser/ui/hns_wallet" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "asset_discovery_manager_unittest.cc",
    "asset_discovery_task_unittest.cc",
    "blockchain_images_source_unittest.cc",
    "hns_wallet_p3a_unittest.cc",
    "hns_wallet_prefs_unittest.cc",
    "hns_wallet_service_unittest.cc",
    "eth_allowance_manager_unittest.cc",
    "eth_pending_tx_tracker_unittest.cc",
    "filecoin_keyring_unittest.cc",
    "keyring_service_unittest.cc",
  ]
  deps = [
    "//base/test:test_support",
    "//hns/browser/hns_wallet",
    "//hns/browser/hns_wallet:tab_helper",
    "//hns/common",
    "//hns/components/hns_wallet/browser",
    "//hns/components/hns_wallet/browser:constants",
    "//hns/components/hns_wallet/browser:hd_keyring",
    "//hns/components/hns_wallet/browser:permission_utils",
    "//hns/components/hns_wallet/browser:pref_names",
    "//hns/components/hns_wallet/browser:test_support",
    "//hns/components/hns_wallet/browser:transaction",
    "//hns/components/hns_wallet/browser:utils",
    "//hns/components/hns_wallet/common",
    "//hns/components/hns_wallet/common:mojom",
    "//hns/components/hns_wallet/common:test_support",
    "//hns/components/filecoin/rs:rust_lib",
    "//hns/components/l10n/common",
    "//hns/components/resources:strings_grit",
    "//hns/components/version_info",
    "//chrome/browser",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]
  if (!is_android) {
    sources += [
      "hns_wallet_tab_helper_unittest.cc",
      "ethereum_provider_impl_unittest.cc",
      "notifications/wallet_notification_service_unittest.cc",
      "solana_provider_impl_unittest.cc",
    ]
    deps += [
      ":hns_wallet_delegate",
      "//hns/components/hns_wallet/common",
      "//hns/components/hns_wallet/common:common_constants",
    ]
    if (enable_extensions) {
      sources += [ "external_wallets_importer_unittest.cc" ]
      deps += [ ":external_wallets_importer" ]
    }
  }
}

source_set("browser_tests") {
  testonly = true
  if (!is_android) {
    sources = [ "hns_wallet_policy_browsertest.cc" ]

    deps = [
      ":hns_wallet",
      "//base/test:test_support",
      "//hns/app:command_ids",
      "//hns/browser",
      "//hns/browser/ui/sidebar",
      "//hns/components/hns_wallet/common",
      "//hns/components/hns_wallet/common:pref_names",
      "//hns/components/sidebar",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/policy/core/browser",
      "//components/policy/core/common:test_support",
      "//components/prefs:prefs",
      "//components/user_prefs:user_prefs",
      "//content/test:test_support",
      "//testing/gtest",
      "//url",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}  # source_set("browser_tests")
