# Copyright (c) 2017 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/build/config.gni")
import("//build/buildflag_header.gni")
import("//build/util/branding.gni")
import("//extensions/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

hns_common_visibility_targets = [
  ":*",
  "//hns:browser_dependencies",
  "//hns:child_dependencies",
  "//hns/browser/*",
  "//hns/ios/browser/*",
  "//hns/renderer/*",
  "//hns/test/*",
  "//hns/utility/*",
  "//chrome/common/*",
  "//chrome/browser/*",
  "//chrome/renderer/*",
  "//chrome/utility/*",
]

source_set("channel_info") {
  visibility = hns_common_visibility_targets

  if (is_linux) {
    sources = [
      "hns_channel_info_posix.cc",
      "hns_channel_info_posix.h",
    ]

    deps = [
      "//base:base",
      "//base:logging_buildflags",
      "//components/version_info:version_info",
    ]

    configs += [ "//hns/build/linux:linux_channel_names" ]
  }
}

source_set("common") {
  visibility = hns_common_visibility_targets +
               # These are layering violation and need to be fixed
               [ "//hns/components/hns_ads/browser" ]
  if (!is_android && !is_ios) {
    visibility += [ "//hns/components/ipfs/test:hns_ipfs_browser_tests" ]
  }

  deps = []

  sources = [
    "hns_channel_info.cc",
    "hns_channel_info.h",
    "resource_bundle_helper.cc",
    "resource_bundle_helper.h",
  ]

  public_deps = [ "//hns/components/constants" ]

  if (!is_android && !is_ios) {
    deps += [ "//hns/common/importer" ]
  }

  if (enable_extensions) {
    sources += [
      "extensions/hns_extensions_api_provider.cc",
      "extensions/hns_extensions_api_provider.h",
      "extensions/hns_extensions_client.cc",
      "extensions/hns_extensions_client.h",
    ]

    deps += [
      "//hns/components/hns_component_updater/browser",
      "//hns/components/https_upgrade_exceptions/browser",
      "//extensions/common:common_constants",
    ]

    public_deps += [ "extensions/api" ]
  }

  deps += [
    ":mojo_bindings",
    "//base",
    "//hns/chromium_src:common",
    "//hns/extensions:common",
    "//components/version_info:channel",
    "//extensions/buildflags",
    "//services/service_manager",
    "//ui/base",
  ]

  if (!is_ios) {
    sources += [
      "hns_content_client.cc",
      "hns_content_client.h",
    ]

    deps += [
      "//hns/components/resources",
      "//hns/components/resources:static_resources_grit",
      "//chrome/common",
      "//chrome/common:channel_info",
      "//chrome/common:constants",
      "//components/resources",
      "//content/public/common",
      "//third_party/widevine/cdm:buildflags",
    ]
  }
}

config("constants_configs") {
  visibility = hns_common_visibility_targets +
               # These are layering violation and need to be fixed
               [ "//chrome/tools/build/mac:infoplist_strings_util" ]
  defines = []
  if (is_mac) {
    defines += [ "HNS_PRODUCT_STRING=\"$chrome_product_full_name\"" ]
  }
}

mojom("mojo_bindings") {
  sources = [ "hns_renderer_configuration.mojom" ]

  deps = [ "//mojo/public/mojom/base" ]
}
