# Copyright (c) 2018 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/components/common/typescript.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

declare_args() {
  hns_sync_endpoint = ""
}

if (is_official_build) {
  assert(hns_sync_endpoint != "")
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "HNS_SYNC_ENDPOINT=\"$hns_sync_endpoint\"" ]
}

static_library("crypto") {
  sources = [
    "crypto/crypto.cc",
    "crypto/crypto.h",
  ]

  deps = [
    "//base",
    "//hns/third_party/bip39wally-core-native:bip39wally-core",
    "//hns/vendor/bat-native-tweetnacl:tweetnacl",
    "//crypto",
    "//third_party/boringssl",
  ]

  if (is_android) {
    deps += [ "//third_party/cpu_features:ndk_compat" ]
  }
}

source_set("features") {
  sources = [
    "features.cc",
    "features.h",
  ]

  deps = [ "//base" ]
}

static_library("network_time_helper") {
  # Remove when https://github.com/hnsvn/hns-browser/issues/10650 is resolved
  check_includes = false
  sources = [
    "network_time_helper.cc",
    "network_time_helper.h",
  ]

  deps = [
    "//base",
    "//components/network_time",
  ]

  if (is_ios) {
    deps += [ "//ios/web/public" ]
  }
}

source_set("p3a") {
  sources = [
    "hns_sync_p3a.cc",
    "hns_sync_p3a.h",
  ]

  deps = [
    "//base",
    "//hns/components/p3a_utils",
    "//components/sync/base",
  ]
}

source_set("prefs") {
  sources = [
    "hns_sync_prefs.cc",
    "hns_sync_prefs.h",
  ]

  deps = [
    "//base",
    "//components/os_crypt/sync",
    "//components/prefs",
  ]
}

source_set("time_limited_codes") {
  sources = [
    "qr_code_data.cc",
    "qr_code_data.h",
    "qr_code_validator.cc",
    "qr_code_validator.h",
    "time_limited_words.cc",
    "time_limited_words.h",
  ]

  deps = [
    ":crypto",
    "//base",
  ]
}

static_library("sync_service_impl_helper") {
  sources = [
    "sync_service_impl_helper.cc",
    "sync_service_impl_helper.h",
  ]

  deps = [
    "//base",
    "//components/sync/service",
    "//components/sync_device_info",
  ]
}

group("hns_sync") {
  deps = [
    ":crypto",
    ":features",
    ":network_time_helper",
    ":prefs",
    ":time_limited_codes",
    "//base",
  ]
  public_deps = [ ":p3a" ]
}

group("constants") {
  public_deps = [ ":buildflags" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "//hns/components/hns_sync/hns_sync_p3a_unittest.cc",
    "//hns/components/hns_sync/hns_sync_prefs_unittest.cc",
    "//hns/components/hns_sync/qr_code_data_unittest.cc",
    "//hns/components/hns_sync/qr_code_validator_unittest.cc",
    "//hns/components/hns_sync/time_limited_words_unittest.cc",
  ]

  deps = [
    ":p3a",
    ":prefs",
    ":time_limited_codes",
    "//base/test:test_support",
    "//components/os_crypt/sync:test_support",
    "//components/prefs:test_support",
    "//components/sync/base",
  ]
}
