# Copyright (c) 2023 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/components/common/typescript.gni")
import("//hns/resources/hns_grit.gni")
import("//tools/grit/repack.gni")

pack_web_resources("page_generated_resources") {
  resource_name = "hns_rewards_page"
  output_dir = "$root_gen_dir/hns/components/hns_rewards/resources"

  # need webpack and GRD generation first
  deps = [ "page" ]
}

pack_web_resources("internals_generated_resources") {
  resource_name = "hns_rewards_internals"
  output_dir = "$root_gen_dir/hns/components/hns_rewards/resources"

  # need webpack and GRD generation first
  deps = [ "internals" ]
}

hns_grit("static_resources") {
  source = "hns_rewards_static_resources.grd"

  outputs = [
    "grit/hns_rewards_resources.h",
    "hns_rewards_static.pak",
  ]
}

repack("resources") {
  public_deps = [
    ":internals_generated_resources",
    ":page_generated_resources",
    ":static_resources",
  ]

  sources = [
    "$root_gen_dir/hns/components/hns_rewards/resources/hns_rewards_internals_generated.pak",
    "$root_gen_dir/hns/components/hns_rewards/resources/hns_rewards_page_generated.pak",
    "$root_gen_dir/hns/components/hns_rewards/resources/hns_rewards_static.pak",
  ]

  if (!is_android) {
    sources += [
      "$root_gen_dir/hns/components/hns_rewards/resources/hns_rewards_panel_generated.pak",
      "$root_gen_dir/hns/components/hns_rewards/resources/tip_panel_generated.pak",
    ]
    public_deps += [
      "rewards_panel:hns_rewards_panel_generated",
      "tip_panel:tip_panel_generated",
    ]
  }

  output = "$root_gen_dir/hns/components/hns_rewards/resources/hns_rewards_resources.pak"
}
