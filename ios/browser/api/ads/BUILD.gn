# Copyright (c) 2021 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.

import("//hns/build/ios/mojom/mojom_wrappers.gni")
import("//build/config/ios/rules.gni")

config("external_config") {
  visibility = [ ":*" ]
  include_dirs = [ "$target_gen_dir" ]
}

source_set("ads") {
  configs += [ ":external_config" ]

  sources = [
    "ads_client_bridge.h",
    "ads_client_ios.h",
    "ads_client_ios.mm",
    "hns_ads.h",
    "hns_ads.mm",
    "inline_content_ad_ios.h",
    "inline_content_ad_ios.mm",
    "notification_ad_ios.h",
    "notification_ad_ios.mm",
  ]

  deps = [
    ":ads_resources",
    ":hns_ads_mojom_wrappers",
    "//base",
    "//hns/components/hns_ads/core",
    "//hns/components/hns_ads/core",
    "//hns/components/hns_federated/public/interfaces",
    "//hns/components/hns_news/common",
    "//hns/components/hns_rewards/common",
    "//hns/components/ntp_background_images/common",
    "//hns/ios/browser/api/common",
  ]

  frameworks = [
    "Foundation.framework",
    "UIKit.framework",
    "Network.framework",
  ]
}

ios_objc_mojom_wrappers("hns_ads_mojom_wrappers") {
  mojom_target = "//hns/components/hns_ads/core/mojom"
  sources = [ "//hns/components/hns_ads/core/mojom/hns_ads.mojom" ]
  exclude_types = [
    "DBCommandInfo",
    "DBCommandBindingInfo",
    "DBCommandResponseInfo",
    "DBCommandResult",
    "DBRecordInfo",
    "DBTransactionInfo",
    "DBValue",
  ]

  # The module name is `HnsAds` now which matches the class we already have
  generate_namespace = false
}

bundle_data("ads_resources") {
  sources = [ "//hns/components/hns_ads/resources/catalog-schema.json" ]
  outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
}
