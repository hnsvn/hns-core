# Copyright (c) 2023 The Hns Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//hns/components/hns_webtorrent/browser/buildflags/buildflags.gni")
import("//hns/components/tor/buildflags/buildflags.gni")
import("//build/config/locales.gni")
import("//chrome/common/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/repack.gni")
import("//ui/base/ui_features.gni")

# Generates a rule to repack a set of resources, substituting a given string
# in for the percentage (e.g. "100", "200"). It generates the repacked files in
# the "gen" directory, and then introduces a copy rule to copy it to the root
# build directory.
#
# Argument:
#   percent [required]
#      String to substitute for the percentage.
template("hns_repack_percent") {
  percent = invoker.percent

  repack(target_name) {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "repack_whitelist",
                             "visibility",
                           ])

    # All sources should also have deps for completeness.
    sources =
        [ "$root_gen_dir/hns/hns_theme_resources_${percent}_percent.pak" ]

    deps = [ "//hns/app/theme:hns_theme_resources" ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    output = "${invoker.output_dir}/hns_${percent}_percent.pak"
  }
}

template("hns_extra_paks") {
  repack(target_name) {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "repack_whitelist",
                             "visibility",
                           ])
    output = "${invoker.output_dir}/hns_resources.pak"
    sources = [
      "$root_gen_dir/hns/components/hns_ads/resources/hns_ads_resources.pak",
      "$root_gen_dir/hns/components/hns_rewards/resources/hns_rewards_resources.pak",
      "$root_gen_dir/components/hns_components_resources.pak",
    ]

    if (!is_android) {
      sources += [
        "$root_gen_dir/hns/hns_generated_resources.pak",
        "$root_gen_dir/hns/hns_unscaled_resources.pak",
        "$root_gen_dir/hns/browser/resources/settings/hns_settings_resources.pak",
      ]
    }

    deps = [
      "//hns/components/hns_ads/resources",
      "//hns/components/hns_rewards/resources",
      "//hns/components/resources",
    ]

    if (!is_android) {
      deps += [
        "//hns/app:hns_generated_resources_grit",
        "//hns/app/theme:hns_unscaled_resources",
        "//hns/browser/resources/settings:resources",
        "//hns/components/resources:strings",
      ]
    }

    if (enable_tor) {
      sources +=
          [ "$root_gen_dir/hns/components/tor/resources/tor_resources.pak" ]

      deps += [ "//hns/components/tor/resources" ]
    }

    if (enable_hns_webtorrent) {
      sources += [ "$root_gen_dir/hns/components/hns_webtorrent/hns_webtorrent_resources.pak" ]

      deps += [ "//hns/components/hns_webtorrent:resources" ]
    }

    if (enable_extensions) {
      sources += [ "$root_gen_dir/hns/components/hns_extension/hns_extension_resources.pak" ]

      deps += [
        "//hns/common/extensions/api",
        "//hns/components/hns_extension:resources",
      ]
    }

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
    if (defined(invoker.additional_paks)) {
      sources += invoker.additional_paks
    }
  }
}

# Defines repack() targets used by Hns. Specifically:
# * hns_100_percent.pak
# * hns_200_percent.pak (optionally)
# * resources.pak
#
# Paramters:
#   output_dir [required]: Directory to output .pak files.
#   additional_extra_paks: List of extra .pak sources for resources.pak.
#   locale_whitelist: if set, override repack_whitelist for locale .pak files.
#   copy_data_to_bundle:
#   deps:
#   output_dir:
#   public_deps:
#   repack_whitelist:
#   visibility:
#     Normal meanings.
#
template("hns_paks") {
  if (!is_android) {
    hns_repack_percent("${target_name}_100_percent") {
      percent = "100"
      forward_variables_from(invoker,
                             [
                               "copy_data_to_bundle",
                               "deps",
                               "output_dir",
                               "repack_whitelist",
                               "visibility",
                             ])
    }

    if (enable_hidpi) {
      hns_repack_percent("${target_name}_200_percent") {
        percent = "200"
        forward_variables_from(invoker,
                               [
                                 "copy_data_to_bundle",
                                 "deps",
                                 "output_dir",
                                 "repack_whitelist",
                                 "visibility",
                               ])
      }
    }
  }

  hns_extra_paks("${target_name}_extra") {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "deps",
                             "output_dir",
                             "repack_whitelist",
                             "visibility",
                           ])
    if (defined(invoker.additional_extra_paks)) {
      additional_paks = invoker.additional_extra_paks
    }
  }

  group(target_name) {
    forward_variables_from(invoker, [ "deps" ])
    public_deps = [ ":${target_name}_extra" ]

    if (!is_android) {
      public_deps += [ ":${target_name}_100_percent" ]
      if (enable_hidpi) {
        public_deps += [ ":${target_name}_200_percent" ]
      }
    }

    if (defined(invoker.public_deps)) {
      public_deps += invoker.public_deps
    }
  }
}
